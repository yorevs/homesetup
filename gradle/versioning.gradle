/*
    Gradle Versioning extension

    Created: 4th March, 2021
    License: MIT - Please refer to <https://opensource.org/licenses/MIT>
    Copyright (c) 2025, HomeSetup team
    This module uses python module bumpver: https://pypi.org/project/bumpver/

    Notice: Following Semantic Versioning 2.0.0 => https://semver.org/
    - Increment the MAJOR version when making incompatible API changes
    - Increment the MINOR version when adding functionality in a backwards-compatible manner
    - Increment the PATCH version when making backwards-compatible bug fixes. (Source)
*/

String relPath(String absPath) {
  return absPath.replace("${project.projectDir}/", '')
}

/* Tasks ------------------------------------------------------------------- */

/* Increment the build number (patch) */
task patchVersion(type: Task) {
  group = 'Versioning'
  description = 'Increment the build number for publishing (patch)'
  outputs.upToDateWhen { false }
  doLast {
    println "Patching the build number"
    exec {
      commandLine rootProject.python, '-m', 'bumpver', 'update', '--patch'
    }
  }
}

/* Increment the build number (minor) */
task updateMinor(type: Task) {
  group = 'Versioning'
  description = 'Increment the build number (minor)'
  outputs.upToDateWhen { false }
  doLast {
    println "Updating the minor number"
    exec {
      commandLine rootProject.python, '-m', 'bumpver', 'update', '--minor'
    }
  }
}

/* Increment the build number (major) */
task updateMajor(type: Task) {
  group = 'Versioning'
  description = 'Increment the build number (major)'
  outputs.upToDateWhen { false }
  doLast {
    println "Updating the major number"
    exec {
      commandLine rootProject.python, '-m', 'bumpver', 'update', '--major'
    }
  }
}

/* Check current program version */
task version(type: Task) {
  group = 'Versioning'
  description = 'Check current program version'
  outputs.upToDateWhen { false }
  doLast { task ->
    println "Project: [$task.project.name] Current Version: $project.ext.app_version"
  }
}

/* List all properties from properties.gradle */
task listProperties(type: Task) {
  group = 'Versioning'
  description = 'List all properties from gradle.properties'
  outputs.upToDateWhen { false }
  doLast {
    def regex = /.*=.*$/
    File propsFile = project.file('gradle.properties')
    if (propsFile.exists()) {
      println "\n[${project.name}] Checking properties from: " + relPath("${propsFile}") + "\n"
      propsFile.eachLine { line ->
        if (line?.trim() && line ==~ regex) {
          // Extract the name and version from line: '<property> = <value>'
          def (name, value) = line.tokenize('=')
          println "${name.padRight(40, ' ')} => ${value.padRight(10, ' ')}"
        }
      }
    }
  }
}
